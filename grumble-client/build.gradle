plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

group = 'gg.grumble.client'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.GRAAL_VM
    }
}

tasks.register('runAgentManual', JavaExec) {
    group = 'application'
    description = 'Run app with GraalVM native-image-agent (manually added)'

    mainClass.set('gg.grumble.client.GrumbleLauncher')
    classpath = sourceSets.main.runtimeClasspath

    jvmArgs = [
            "-agentlib:native-image-agent=config-output-dir=build/native/agent-output/runAgentManual",
            "-Dspring.aot.enabled=false"
    ]
}

graalvmNative {
    toolchainDetection = false

    binaries {
        main {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(21)
                vendor = JvmVendorSpec.GRAAL_VM
            }

            buildArgs.addAll([
                    "-H:+UnlockExperimentalVMOptions",
                    "-H:TraceClassInitialization=com.sun.media.sound.PortMixerProvider",
                    "-H:ClassInitialization=com.sun.media.sound.PortMixerProvider:run_time",
                    "-H:ClassInitialization=com.sun.media.sound.DirectAudioDeviceProvider:run_time",
                    "-H:ConfigurationFileDirectories=build/native/agent-output/runAgentManual",
                    "-H:+AddAllCharsets",
                    "-H:+ReportExceptionStackTraces"
            ])

            resources {
                includedPatterns.add("natives/.*")
                includedPatterns.add("META-INF/services/.*")
            }

            runtimeArgs.add('-Djava.home=.')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
}

javafx {
    version = '21.0.7'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation("org.springframework:spring-webflux")

    // grumble
    implementation project(":grumble-core")

    // jansi colors
    implementation 'org.fusesource.jansi:jansi:2.4.2'

    // richtextfx
    implementation 'org.fxmisc.richtext:richtextfx:0.11.5'
}

test {
    useJUnitPlatform()
}